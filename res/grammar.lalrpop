// This grammar has been adapted from
// https://craftinginterpreters.com/appendix-i.html#syntax-grammar.

use lalrpop_util::ParseError;
use crate::error::ErrorS;

use crate::syntax::{ast, lexer};

grammar<'err>(
    errors: &'err mut Vec<ParseError<usize, lexer::Token, ErrorS>>
);

pub Program: ast::Program = <stmts:Spanned<Decl>*> => ast::Program { <> };

// Declarations
Decl = {
    DeclClass,
    DeclFn,
    DeclLet,
    DeclConst,
    Stmt,
}

DeclClass: ast::Stmt = <class:StmtClass> => ast::Stmt::Class(<>);

StmtClass: ast::StmtClass =
    "class" <name:identifier> <super_:("extends" <Spanned<ExprIdentifier>>)?> "{"
        <fields:(<Spanned<StmtLet>>)*>
        <methods:(<Spanned<StmtFn>>)*>
    "}" =>
        ast::StmtClass { <> };

DeclFn: ast::Stmt = <function:StmtFn> => ast::Stmt::Fn(<>);

StmtFn: ast::StmtFn = {
    // Function statement with a block statement as the body
    "fn" <name:identifier> "(" <params:Params> ")" <body:StmtBlockInternal> =>
        ast::StmtFn { <> },
    // Function statement with an expression statement as the body
    "fn" <name:identifier> "(" <params:Params> ")" "=" <body:Spanned<StmtExpr>> => {
        let (body_stmt, body_span) = body;

        // Get the value from the expression statement body
        let body_stmt_value = match body_stmt {
            ast::Stmt::Expr(expr) => Some(expr.value),
            // This shouldn't happen. A parsing error should occur first
            _ => None
        };

        // Transform the function's body from an expression statement to
        // a block statement that return's the expression's value
        let body = ast::StmtBlock {
            stmts: vec![
                (
                    ast::Stmt::Return(ast::StmtReturn { 
                        value: body_stmt_value
                    }),
                    body_span
                )
            ]
        };

        ast::StmtFn {
            name,
            params,
            body
        }
    }
}

DeclConst: ast::Stmt = <assign:StmtConst> =>
    ast::Stmt::Const(<>);

StmtConst: ast::StmtConst = "const" <name:identifier> <value:("=" <ExprS>)?> ";" => ast::StmtConst {
    identifier: ast::Identifier {
        name,
        depth: None
    },
    value,
};

DeclLet: ast::Stmt = <assign:StmtLet> =>
    ast::Stmt::Let(<>);

StmtLet: ast::StmtLet = "let" <name:identifier> <value:("=" <ExprS>)?> ";" => ast::StmtLet {
    identifier: ast::Identifier {
        name,
        depth: None
    },
    value,
};

// Statements
// https://en.wikipedia.org/wiki/Dangling_else#Avoiding_the_conflict_in_LR_parsers
Stmt: ast::Stmt = {
    StmtOpen,
    StmtClosed,
    <e:!> => {
        errors.push(e.error);
        ast::Stmt::Error
    },
}

StmtOpen: ast::Stmt = {
    "if" "(" <cond:ExprS> ")" <then:Spanned<Stmt>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { <>, else_: None })),
    "if" "(" <cond:ExprS> ")" <then:Spanned<StmtClosed>> "else" <else_:Spanned<StmtOpen>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:Spanned<StmtOpen>> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:Spanned<StmtOpen>> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
}

StmtClosed: ast::Stmt = {
    "if" "(" <cond:ExprS> ")" <then:Spanned<StmtClosed>> "else" <else_:Spanned<StmtClosed>> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:ExprS> ")" <body:Spanned<StmtClosed>> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:Spanned<StmtClosed>> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
    StmtSimple,
}

ForInit: Option<ast::StmtS> = {
    <Spanned<DeclLet>> => Some(<>),
    <Spanned<StmtExpr>> => Some(<>),
    ";" => None,
}

ForCond = <ExprS?> ";";

ForIncr = <ExprS?>;

StmtSimple = {
    StmtBlock,
    StmtExpr,
    StmtPrint,
    StmtReturn,
}

StmtBlock: ast::Stmt = StmtBlockInternal => ast::Stmt::Block(<>);

StmtBlockInternal: ast::StmtBlock = "{" <stmts:Spanned<Decl>*> "}" =>
    ast::StmtBlock { <> };

StmtExpr: ast::Stmt = <value:ExprS> ";" =>
    ast::Stmt::Expr(ast::StmtExpr { <> });

StmtPrint: ast::Stmt = "print" <value:ExprS> ";" =>
    ast::Stmt::Print(ast::StmtPrint { <> });

StmtReturn: ast::Stmt = "return" <value:ExprS?> ";" =>
    ast::Stmt::Return(ast::StmtReturn { <> });

// Expressions
ExprS = Spanned<Expr>;

Expr = ExprAssign;

ExprAssign = {
    <name:identifier> "=" <value:ExprS> =>
        ast::Expr::Assign(Box::new(ast::ExprAssign {
            identifier: ast::Identifier {
                name,
                depth: None
            },
            value,
        })),
    <target:Spanned<ExprCall>> "[" <index:number> "]" "=" <value:ExprS> =>
        ast::Expr::SetIndex(Box::new(ast::ExprSetIndex {<>})),
    <object:Spanned<ExprCall>> "." <name:identifier> "=" <value:ExprS> =>
        ast::Expr::Set(Box::new(ast::ExprSet { <> })),
    <object:Spanned<ExprCall>> "[" <name:identifier> "]" "=" <value:ExprS> =>
        ast::Expr::Set(Box::new(ast::ExprSet { <> })),
    <object:Spanned<ExprCall>> "[" <name:string> "]" "=" <value:ExprS> =>
        ast::Expr::Set(Box::new(ast::ExprSet { <> })),
    ExprLogicOr,
}

ExprLogicOr = ExprInfix<ExprLogicOr, OpLogicOr, ExprLogicAnd>;
OpLogicOr: ast::OpInfix = "or" => ast::OpInfix::LogicOr;

ExprLogicAnd = ExprInfix<ExprLogicAnd, OpLogicAnd, ExprEquality>;
OpLogicAnd: ast::OpInfix = "and" => ast::OpInfix::LogicAnd;

ExprEquality = ExprInfix<ExprEquality, OpEquality, ExprComparison>;
OpEquality: ast::OpInfix = {
    "==" => ast::OpInfix::Equal,
    "!=" => ast::OpInfix::NotEqual,
}

ExprComparison = ExprInfix<ExprComparison, OpComparison, ExprTerm>;
OpComparison: ast::OpInfix = {
    ">" => ast::OpInfix::Greater,
    ">=" => ast::OpInfix::GreaterEqual,
    "<" => ast::OpInfix::Less,
    "<=" => ast::OpInfix::LessEqual,
}

ExprTerm = ExprInfix<ExprTerm, OpTerm, ExprFactor>;
OpTerm: ast::OpInfix = {
    "+" => ast::OpInfix::Add,
    "-" => ast::OpInfix::Subtract,
}

ExprFactor = ExprInfix<ExprFactor, OpFactor, ExprPrefix>;
OpFactor: ast::OpInfix = {
    "*" => ast::OpInfix::Multiply,
    "/" => ast::OpInfix::Divide,
    "%" => ast::OpInfix::Modulus,
}

ExprInfix<Lt, Op, Rt>: ast::Expr = {
    <lt:Spanned<Lt>> <op:Op> <rt:Spanned<Rt>> =>
        ast::Expr::Infix(Box::new(ast::ExprInfix { <> })),
    Rt,
}

ExprPrefix: ast::Expr = {
    <op:OpPrefix> <rt:Spanned<ExprPrefix>> =>
        ast::Expr::Prefix(Box::new(ast::ExprPrefix { <> })),
    ExprCall,
}
OpPrefix: ast::OpPrefix = {
    "-" => ast::OpPrefix::Negate,
    "!" => ast::OpPrefix::Not,
}

ExprCall: ast::Expr = {
    <callee:Spanned<ExprCall>> "(" <args:Args> ")" =>
        ast::Expr::Call(Box::new(ast::ExprCall { callee, args })),
    <object:Spanned<ExprCall>> "." <name:identifier> =>
        ast::Expr::Get(Box::new(ast::ExprGet { <> })),
    <object:Spanned<ExprCall>> "[" <name:string> "]" =>
        ast::Expr::Get(Box::new(ast::ExprGet { <> })),
    <object:Spanned<ExprCall>> "[" <name:identifier> "]" =>
        ast::Expr::Get(Box::new(ast::ExprGet { <> })),
    <target:Spanned<ExprCall>> "[" <index:number> "]" =>
        ast::Expr::GetIndex(Box::new(ast::ExprGetIndex {<>})),
    "super" "." <name:identifier> =>
        ast::Expr::Super(ast::ExprSuper {
            super_: ast::Identifier {
                name: "super".to_string(),
                depth: None,
            },
            name,
        }),
    "super" "[" <name:string> "]" =>
        ast::Expr::Super(ast::ExprSuper {
            super_: ast::Identifier {
                name: "super".to_string(),
                depth: None,
            },
            name,
        }),
    "super" "[" <name:identifier> "]" =>
        ast::Expr::Super(ast::ExprSuper {
            super_: ast::Identifier {
                name: "super".to_string(),
                depth: None,
            },
            name,
        }),
    ExprList,
}

ExprList: ast::Expr = {
    "[" <values:Args> "]" => ast::Expr::Literal(ast::ExprLiteral::List(<>)),
    ExprPrimary,
}

ExprPrimary: ast::Expr = {
    // Literals
    "nil" => ast::Expr::Literal(ast::ExprLiteral::Nil),
    "false" => ast::Expr::Literal(ast::ExprLiteral::Bool(false)),
    "true" => ast::Expr::Literal(ast::ExprLiteral::Bool(true)),
    string => ast::Expr::Literal(ast::ExprLiteral::String(<>)),
    number => ast::Expr::Literal(ast::ExprLiteral::Number(<>)),

    // Variables
    ExprIdentifier,
    ExprThis,    

    // Grouping
    "(" <Expr> ")",
}

ExprIdentifier: ast::Expr = <name:identifier> =>
    ast::Expr::Identifier(ast::ExprIdentifier {
        identifier: ast::Identifier {
            name,
            depth: None
        }
    });

ExprThis: ast::Expr = "this" => ast::Expr::Identifier(ast::ExprIdentifier {
    identifier: ast::Identifier {
        name: "this".to_string(),
        depth: None,
    }
});

/// Utilities

// A statement or expression and it's location/range in the source code
// Used for error reporting and diagnostic messages
Spanned<T>: ast::Spanned<T> = <l:@L> <t:T> <r:@R> => (t, l..r);

// Comma separated list of string parameters a function/method takes
Params: Vec<String> = {
    <first:identifier> <mut params:("," <identifier>)*> => {
        params.insert(0, first);
        params
    },
    () => Vec::new(),
};

// Comma separated list of expression arugments passed to function/method calls
Args: Vec<ast::ExprS> = {
    <first:ExprS> <mut args:("," <ExprS>)*> => {
        args.insert(0, first);
        args
    },
    () => Vec::new(),
}

extern {
    type Location = usize;
    type Error = ErrorS;

    enum lexer::Token {
        // Single-character tokens.
        "(" => lexer::Token::LtParen,
        ")" => lexer::Token::RtParen,
        "{" => lexer::Token::LtBrace,
        "}" => lexer::Token::RtBrace,
        "[" => lexer::Token::LtBracket,
        "]" => lexer::Token::RtBracket,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "%" => lexer::Token::Modulus,
        "*" => lexer::Token::Asterisk,

        // One or two character tokens.
        "!" => lexer::Token::Bang,
        "!=" => lexer::Token::BangEqual,
        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        ">" => lexer::Token::Greater,
        ">=" => lexer::Token::GreaterEqual,
        "<" => lexer::Token::Less,
        "<=" => lexer::Token::LessEqual,

        // Literals.
        identifier => lexer::Token::Identifier(<String>),
        string => lexer::Token::String(<String>),
        number => lexer::Token::Number(<f64>),

        // Keywords.
        "and" => lexer::Token::And,
        "class" => lexer::Token::Class,
        "else" => lexer::Token::Else,
        "false" => lexer::Token::False,
        "for" => lexer::Token::For,
        "fn" => lexer::Token::Fn,
        "if" => lexer::Token::If,
        "nil" => lexer::Token::Nil,
        "or" => lexer::Token::Or,
        "print" => lexer::Token::Print,
        "return" => lexer::Token::Return,
        "super" => lexer::Token::Super,
        "this" => lexer::Token::This,
        "true" => lexer::Token::True,
        "let" => lexer::Token::Let,
        "const" => lexer::Token::Const,
        "while" => lexer::Token::While,
        "extends" => lexer::Token::Extends,
    }
}
